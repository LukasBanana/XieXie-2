/*
 * Integer.xx
 * 
 * This file is part of the "XieXie-Compiler" (Copyright (c) 2014 by Lukas Hermanns)
 * See "LICENSE.txt" for license information.
 */

import ASCII

/// Integer class.
class Integer {

	init() {}
	init(int value) {
		this.value := value
	}
	init(String s) {
		value := Integer.parseFrom(s)
	}
	
	[[override]]
	String toString() {	
		// Determine string length
		int len, firstDigit, v := value
		
		if v < 0 {
			len++
			firstDigit := 1
			v := -v
		}
		
		do {
			len++
			v /= 10
		} while v > 0
		
		// Allocate string
		var s := new String(len)
		
		// Check for negative value
		v := value
		
		if v < 0 {
			// Append sign to string
			s.setChar(0, ASCII.HYPHEN)
			v := -v
		}
		
		// Append digits to string
		for int i := len - 1 ; i >= firstDigit ; i-- {
			s.setChar(i, ASCII.DIGIT0 + (v & 9))
			v /= 10
		}
		
		return s
	}
	
	/// Parses an integer from the specified string
	static int parseFrom(String s) {
		if s = null {
			// Invalid argument
			return 0
		}
		
		int i, v, digit, len := s.size()
		bool neg
		
		// Check if integer is negative
		if len > 0 and s.getChar(0) = ASCII.HYPHEN {
			neg := true
			i++
		}
		
		// Parse digits
		for ; i < len ; i++ {
			digit := s.getChar(i)
			
			if digit < ASCII.DIGIT0 or digit > ASCII.DIGIT9 {
				// Error while parsing
				return 0
			}
			
			// Shift previous digit to left (base 10)
			v *= 10
			
			// Append digit to integer
			v += (digit - ASCII.DIGIT0)
		}
		
		// Negate value
		if neg {
			v := -v
		}
		
		return v
	}
	
	/// Returns a pointer to the integer value.
	[[final]]
	int pointer() {
		return pointerOffset(0)
	}
	
	int value
	
}

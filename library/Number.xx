/*
 * Number.xx
 * 
 * This file is part of the "XieXie-Compiler" (Copyright (c) 2014 by Lukas Hermanns)
 * See "LICENSE.txt" for license information.
 */

/**
Number base class.
@see Integer
@see Float
@since 2.00
@author Lukas Hermanns
*/
class Number {
	
	static const uncomparable := 666
	
	/**
	@return -1 if this number is less than 'rhs',
	0 if this number is equal to 'rhs', or 1 if this number is greater than 'rhs'.
	*/
	int compareTo(Number rhs)
	
	/// @return This number as integer.
	int toInteger()
	/// @return This number as floating-point.
	float toFloat()
	
	/**
	@return True if this number is equal to 'rhs'.
	@remarks This overrides the Object.equals procedure, but actually only uses the Number.equal procedure.
	@see equal
	*/
	[[override]]
	bool equals(Object rhs) {
		return rhs is Number ? equal((Number)rhs) : false
	}
	
	/// @return True if this number is equal to 'rhs'.
	bool equal(Number rhs) {
		return compareTo(rhs) = 0
	}
	
	/// @return True if this number is greater than 'rhs'.
	bool greater(Number rhs) {
		return compareTo(rhs) > 0
	}
	
	/// @return True if this number is greater than or equal to 'rhs'.
	bool greaterOrEqual(Number rhs) {
		return compareTo(rhs) >= 0
	}
	
	/// @return True if this number is less than 'rhs'.
	bool less(Number rhs) {
		return compareTo(rhs) < 0
	}
	
	/// @return True if this number is less than or equal to 'rhs'.
	bool lessOrEqual(Number rhs) {
		return compareTo(rhs) <= 0
	}
	
	/**
	Computes the signum function for the specified integer value.
	@return -1 if value is less than zero, 0 if value is equal to zero, or 1 if value is greater than zero.
	*/
	static int signum(int value) {
		return value < 0 ? -1 :
			   value > 0 ?  1 : 0
	}
	
	/**
	Computes the signum function for the specified floating-point value.
	@return -1 if value is less than zero, 0 if value is equal to zero, or 1 if value is greater than zero.
	*/
	static int signum(float value) {
		return value < 0.0 ? -1 :
			   value > 0.0 ?  1 : 0
	}
	
}

/*
 * SystemInfo.xx
 * 
 * This file is part of the "XieXie-Compiler" (Copyright (c) 2014 by Lukas Hermanns)
 * See "LICENSE.txt" for license information.
 */

import Integer

//! System information module.
[[bind]]
module SysInfo {
	
	static void querySystemInfo(
		int platformStringPtr,
		int cpuArchStringPtr,
		int cpuTypeStringPtr,
		int cpuNameStringPtr,
		int cpuCountIntegerPtr,
		int cpuSpeedIntegerPtr
	)
	
}

/// System information structure.
class SystemInfo {
	
	/// Queries the system information from the "SysInfo" module.
	static SystemInfo query() {
		var info := new SystemInfo()
		
		/*static*/ const maxLen := 64
		
		// Allocate strings
		info.platform	:= new String(maxLen)
		info.cpuArch	:= new String(maxLen)
		info.cpuType	:= new String(maxLen)
		info.cpuName	:= new String(maxLen)
		var cpuCount	:= new Integer()
		var cpuSpeed	:= new Integer()
		
		// Query system information from module
		SysInfo.querySystemInfo(
			info.platform.pointer(),
			info.cpuArch.pointer(),
			info.cpuType.pointer(),
			info.cpuName.pointer(),
			cpuCount.pointer(),
			cpuSpeed.pointer()
		)
		
		// Copy integer values
		info.cpuCount := cpuCount.value
		info.cpuSpeed := cpuSpeed.value
		
		// Adjust strings
		info.platform.adjust()
		info.cpuArch.adjust()
		info.cpuType.adjust()
		info.cpuName.adjust()
		
		return info
	}
	
	/// Platform name, e.g. "Windows 7".
	String platform
	/// CPU architecture, e.g. "x86-64".
	String cpuArch
	/// CPU type, e.g. "64 bit".
	String cpuType
	/// CPU name, e.g. "Intel Core i7-3770K"
	String cpuName
	/// Number of processors.
	int cpuCount
	/// Processor speed (in MHz).
	int cpuSpeed
	
}


/*
 * SystemInfo.xx
 * 
 * This file is part of the "XieXie-Compiler" (Copyright (c) 2014 by Lukas Hermanns)
 * See "LICENSE.txt" for license information.
 */

import Integer

/// System information module.
[[bind]]
private module SysInfo {
	
	static void querySystemInfo(
		String platform,
		String cpuArch,
		String cpuType,
		String cpuName,
		Integer cpuCount,
		Integer cpuSpeed
	)
	
}

/// System information structure.
class SystemInfo {
	
	/// Queries the system information from the "SysInfo" module.
	static SystemInfo query() {
		var info := new SystemInfo()
		
		// Allocate strings
		info.platform	:= new String()
		info.cpuArch	:= new String()
		info.cpuType	:= new String()
		info.cpuName	:= new String()
		var cpuCount	:= new Integer()
		var cpuSpeed	:= new Integer()
		
		// Query system information from module
		SysInfo.querySystemInfo(
			platform:	info.platform,
			cpuArch:	info.cpuArch,
			cpuType:	info.cpuType,
			cpuName:	info.cpuName,
			cpuCount:	cpuCount,
			cpuSpeed:	cpuSpeed
		)
		
		// Copy integers
		info.cpuCount := cpuCount.value
		info.cpuSpeed := cpuSpeed.value
		
		return info
	}
	
	/// Platform name, e.g. "Windows 7".
	String platform
	/// CPU architecture, e.g. "x86-64".
	String cpuArch
	/// CPU type, e.g. "64 bit".
	String cpuType
	/// CPU name, e.g. "Intel Core i7-3770K"
	String cpuName
	/// Number of processors.
	int cpuCount
	/// Processor speed (in MHz).
	int cpuSpeed
	
}

